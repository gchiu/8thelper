\ vim: syntax=forth

true app:isgui !

var db

: dbpath \ -- s
	app:8thdir "docs/help.db" s:+ ;

: ok?
	dbpath
	f:exists? ;

: quit
	db @ db:close
	g:quit ;

: opendb
	dbpath db:open db ! ;
	
: update \ gui text --
	swap "content" g:child
	swap g:text g:invalidate ;

: sqlgen \ keyword -- sql
	s:len if
		":" s:/ a:rev
	else
		drop []
	then
	( "%" swap s:+ "%" s:+ ) a:map
	a:len >r a:open
	[ "select cls, num, st, desc from words limit 0", "select cls, nm, st, desc from words where nm like '%s'", "select cls, nm, st, desc from words where cls='%s' and nm like '%s'", ]
	r> a:@ nip
	s:strfmt ;

: search \ keyword -- text
	"" >r
	sqlgen
	db @
	( db:col{} "%cls%:%nm%\t\\ %st%\n%desc%\n\n" swap s:tsub r> swap s:+ >r )
	rot db:exec-cb 
	drop
	r> ;

: keypressed	\ gui keycode character --
	nip dup
	0x08 n:= if
		drop
		g:text? s:len 0 n:> if
			s:len 1 n:- 1 s:- g:text
		else
			drop
		then
	else
		dup 0x21 0x7e n:between if
			swap
			g:text? rot s:+ g:text
		else
			drop
		then
	then 
	g:text? search update ;

: app:main
	ok? if
		opendb
		"window.json" app:asset G:eval
		g:new g:show
	else
		"You should install 8th on your system first!" . cr bye
	then ;
